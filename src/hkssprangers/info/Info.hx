package hkssprangers.info;

using hkssprangers.info.Info.TgTools;
using hkssprangers.info.Info.OrderTools;
using Lambda;

abstract Cents(Int) from Int to Int {
    public function print() {
        return "$" + (this * 0.01);
    }

    @:op(A + B)
    static function add(a:Cents, b:Cents):Cents return (a:Int) + (b:Int);

    @:op(A - B)
    static function minus(a:Cents, b:Cents):Cents return (a:Int) + (b:Int);
}

enum abstract Shop<T>(String) {
    var EightyNine:Shop<EightyNineItem>;
    var DragonJapaneseCuisine:Shop<Dynamic>;
    var YearsHK:Shop<Dynamic>;

    public function info() return switch (cast this:Shop<T>) {
        case EightyNine:
            {
                id: EightyNine,
                name: "89ç¾é£Ÿ",
                address: "è¥¿ä¹é¾ä¸­å¿ƒ8æ¨“ç¾é£Ÿå»£å ´9è™Ÿèˆ–",
            }
        case DragonJapaneseCuisine:
            {
                id: DragonJapaneseCuisine,
                name: "ç‡Ÿæ¥­éƒ¨",
                address: "è¥¿ä¹é¾ä¸­å¿ƒ8æ¨“ç¾é£Ÿå»£å ´8F35èˆ–",
            }
        case YearsHK:
            {
                id: YearsHK,
                name: "YearsHK",
                address: "æ·±æ°´åŸ—ç¦è¯è¡—191-199è™Ÿç¦éš†å¤§å»ˆ1è™Ÿåœ°èˆ–",
            }
    }
}

enum abstract EightyNineItem(String) {
    var EightyNineSet;

    public function info(data:Dynamic) return switch (cast this:EightyNineItem) {
        case EightyNineSet:
            var dataInfo = {
                main: (data.main:EightyNineSetMain).info(),
                sub: (data.sub:EightyNineSetSub).info(),
                given: (data.given:EightyNineSetGiven).info(),
            };
            {
                id: EightyNineSet,
                name: "å¥—é¤",
                description: "ä¸»èœ + é…èœ + çµ²è‹—ç™½é£¯2å€‹",
                detail:
                    [
                        dataInfo.main != null ? dataInfo.main.name + " " + dataInfo.main.priceCents.print() : "æœªé¸å–ä¸»èœ",
                        dataInfo.sub != null ? dataInfo.sub.name : "æœªé¸å–é…èœ",
                        dataInfo.given.name,
                    ].join("\n"),
                priceCents: {
                    switch (dataInfo) {
                        case {main: main, sub: sub, given: given} if (main != null && sub != null && given != null):
                            main.priceCents;
                        case _:
                            0;
                    }
                }
            }
    }
}

enum abstract EightyNineSetMain(String) {
    var EightyNineSetMain1;
    var EightyNineSetMain2;
    var EightyNineSetMain3;
    var EightyNineSetMain4;
    var EightyNineSetMain5;

    public function info():{
        id: EightyNineSetMain,
        name: String,
        priceCents: Cents,
    } return switch (cast this:EightyNineSetMain) {
        case EightyNineSetMain1:
            {
                id: EightyNineSetMain1,
                name: "é¦™èŒ…è±¬é ¸è‚‰",
                priceCents: 8500,
            }
        case EightyNineSetMain2:
            {
                id: EightyNineSetMain2,
                name: "æ‹›ç‰Œå£æ°´é› (ä¾‹)",
                priceCents: 8500,
            }
        case EightyNineSetMain3:
            {
                id: EightyNineSetMain3,
                name: "å»éª¨æµ·å—é› (ä¾‹)",
                priceCents: 8500,
            }
        case EightyNineSetMain4:
            {
                id: EightyNineSetMain4,
                name: "æ‹›ç‰Œå£æ°´é› (ä¾‹) æ‹¼é¦™èŒ…è±¬é ¸è‚‰",
                priceCents: 9800,
            }
        case EightyNineSetMain5:
            {
                id: EightyNineSetMain5,
                name: "å»éª¨æµ·å—é› (ä¾‹) æ‹¼é¦™èŒ…è±¬é ¸è‚‰",
                priceCents: 9800,
            }
    }
}

enum abstract EightyNineSetSub(String) {
    var EightyNineSetSub1;
    var EightyNineSetSub2;

    public function info() return switch (cast this:EightyNineSetSub) {
        case EightyNineSetSub1:
            {
                id: EightyNineSetSub1,
                name: "æ¶¼æ‹Œé’ç“œæ‹¼æœ¨è€³",
            }
        case EightyNineSetSub2:
            {
                id: EightyNineSetSub2,
                name: "éƒŠå¤–æ²¹èœ",
            }
    }
}

enum abstract EightyNineSetGiven(String) {
    var EightyNineSetGiven1;

    public function info() return switch (cast this:EightyNineSetGiven) {
        case EightyNineSetGiven1:
            {
                id: EightyNineSetGiven1,
                name: "å¥—é¤é™„é€çµ²è‹—ç™½é£¯2å€‹",
            }
    }
}

enum abstract PaymentMethod(String) to String {
    var PayMe;
    var FPS;

    public function info() return switch (cast this:PaymentMethod) {
        case PayMe:
            {
                id: PayMe,
                name: "PayMe",
            }
        case FPS:
            {
                id: FPS,
                name: "FPS",
            }
    }
}

enum abstract PickupMethod(String) to String {
    var Door;
    var Street;

    public function info() return switch (cast this:PickupMethod) {
        case Door:
            {
                id: Door,
                name: "ä¸Šé–€äº¤æ”¶",
            }
        case Street:
            {
                id: Street,
                name: "æ¨“ä¸‹äº¤æ”¶",
            }
    }
}

typedef OrderItem = {
    type: String,
}

typedef Order<Item> = {
    shop:Shop<Item>,
    code:String,
    timestamp:Int,
    items: Array<{
        id: Item,
        data: Dynamic,
    }>,
    wantTableware:Bool,
    customerNote:Null<String>,
}

typedef Tg = {
    ?id: Int,
    ?username: String,
}

typedef Delivery = {
    courier: {
        tg: Tg
    },
    orders:Array<Order<Dynamic>>,
    customer: {
        tg: Tg,
        tel: String,
    },
    paymentMethods: Array<PaymentMethod>,
    pickupLocation: String,
    pickupDate: String,
    pickupTime: String,
    pickupMethod: PickupMethod,
    deliveryFeeCents: Cents,
    customerNote: String,
}

class TgTools {
    static public function print(tg:Tg):String {
        return if (tg.username != null)
            "@" + tg.username;
        else if (tg.id != null)
            "" + tg.id;
        else
            "[none]";
    }
}

class OrderTools {
    static public function totalCents<T>(order:Order<T>):Cents {
        var total:Cents = 0;

        for (item in order.items) {
            switch (order.shop) {
                case EightyNine:
                    switch (item.id:EightyNineItem) {
                        case itemId = EightyNineSet:
                            var info = itemId.info(item.data);
                            total += info.priceCents;
                    }
                case DragonJapaneseCuisine:

                case YearsHK:

            }
        }

        return total;
    }

    static public function print<T>(order:Order<T>):String {
        var buf = new StringBuf();

        buf.add("ğŸ“ƒ " + order.shop.info().name + " " + order.code + " (" + totalCents(order).print() + ")\n");

        for (item in order.items) {
            switch (order.shop) {
                case EightyNine:
                    switch (item.id:EightyNineItem) {
                        case itemId = EightyNineSet:
                            var info = itemId.info(item.data);
                            buf.add(info.detail);
                    }
                case DragonJapaneseCuisine:

                case YearsHK:

            }
        }

        buf.add(order.wantTableware ? "è¦é¤å…·\n" : "å””è¦é¤å…·\n");

        return buf.toString();
    }
}

class DeliveryTools {
    static public function print(d:Delivery):String {
        var buf = new StringBuf();

        buf.add("å¤–è³£å“¡: " + d.courier.tg.print() + "\n");

        for (order in d.orders) {
            buf.add(order.print());
        }

        buf.add("\n");
        var foodTotal = d.orders.fold((order:Order<Dynamic>, result:Cents) -> result + order.totalCents(), 0);
        buf.add("é£Ÿç‰©åƒ¹éŒ¢: " + foodTotal.print() + "\n");
        buf.add("é£Ÿç‰©+é‹è²»: " + (foodTotal + d.deliveryFeeCents).print() + "\n");

        buf.add("\n");
        buf.add("å®¢äººäº¤æ”¶æ™‚æ®µ: " + d.pickupDate + " " + d.pickupTime + "\n");
        buf.add("tg: " + d.customer.tg.print() + "\n");
        buf.add(d.customer.tel + "\n");
        buf.add(d.paymentMethods.map(p -> p.info().name).join(", ") + "\n");
        buf.add(d.pickupLocation + " (" + d.pickupMethod.info().name + ")\n");

        return buf.toString();
    }
}