package hkssprangers;

import tink.sql.expr.Functions;
import comments.CommentString.*;
import thx.Weekday;
import hkssprangers.info.Weekday;
import hkssprangers.info.OrderTools;
import telegraf.Extra;
import telegraf.Telegraf;
import hkssprangers.info.TimeSlot;
import hkssprangers.info.TimeSlotType;
import haxe.Json;
import hkssprangers.server.CockroachDb;
import tink.CoreApi;
import tink.core.ext.Promises;
import hkssprangers.info.Shop;
import hkssprangers.info.Delivery;
import tink.sql.expr.Functions as F;
import hkssprangers.Availability;
import hkssprangers.AvailabilityTools;
using Lambda;
using StringTools;
using hkssprangers.db.DatabaseTools;
using hkssprangers.info.TimeSlotTools;

class CronJobs {
    static final isMain = js.Syntax.code("require.main") == js.Node.module;

    static function sendDutyPoll(chatId:String) {
        var tgBot = new Telegraf(TelegramConfig.tgBotToken);
        var now = Date.now();
        var nextDays = switch (Weekday.fromDay(now.getDay())) {
            case Sunday:
                [
                    Date.fromTime(now.getTime() + DateTools.days(1)),
                    Date.fromTime(now.getTime() + DateTools.days(2)),
                    Date.fromTime(now.getTime() + DateTools.days(3)),
                    Date.fromTime(now.getTime() + DateTools.days(4)),
                ];
            case Thursday:
                [
                    Date.fromTime(now.getTime() + DateTools.days(1)),
                    Date.fromTime(now.getTime() + DateTools.days(2)),
                    Date.fromTime(now.getTime() + DateTools.days(3)),
                ];
            case d:
                throw "Unknown weekday: " + d;
        }

        var slots = [
            for (d in nextDays)
            for (t in [Lunch, Dinner])
            (d.getMonth() + 1) + "æœˆ" + d.getDate() + "æ—¥ (" + Weekday.fromDay(d.getDay()).info().name + ") " + t.info().periodName
        ];

        final msg = comment(unindent)/**
            åˆä¿‚æ™‚å€™éº»ç…©å„ä½å ±ä¸€å ±é‚Šå€‹æ™‚æ®µå¾—é–’å¹«æ‰‹. 
            æœƒå–ºé–‹å¸‚å‰ä¸€å°æ™‚é–‹è¨­å ±åˆ°æŠ•ç¥¨ (åˆå¸‚ 11:00 æ™šå¸‚ 17:00).
            æœƒå„ªå…ˆæ´¾å–®ä¿¾é æ—©å ±å’—ç•¶å€¼å˜…å¤–è³£å“¡.
            äº¦éƒ½å¸Œæœ›é æ—©å ±å’—ç•¶å€¼å˜…å¤–è³£å“¡æœƒæº–æ™‚å‡ºç¾å ±åˆ°æ¥å–® (15åˆ†é˜å…§), å””æƒ³åŠ ç½°å‰‡, è«‹è‡ªå¾‹ ğŸ™
        **/;

        return tgBot.telegram.sendPoll(chatId, msg, slots.concat(["ä»¥ä¸Šæ—¥å­å…¨éƒ¨éƒ½å””å¾—/å””è‚¯å®š"]), {
            is_anonymous: false,
            allows_multiple_answers: true,
        })
            .then(msg -> {
                tgBot.telegram.pinChatMessage(chatId, msg.message_id);
            })
            .then(_ -> null);
    }

    static function setTimeSlots(startDate:LocalDateString, endDate:LocalDateString, setAvailability:TimeSlot->Null<Availability>) {
        final dateStart = startDate.getDatePart();
        final dateEnd = endDate.getDatePart();
        var date = dateStart;
        var slots:Array<TimeSlot> = [];
        while (date <= dateEnd) {
            for (slot in TimeSlotTools.regularTimeSlots) {
                slots.push({
                    start: date + " " + slot.start,
                    end: date + " " + slot.end,
                });
            }
            date = (date + " 00:00:00":LocalDateString).deltaDays(1).getDatePart();
        }

        return CockroachDb.db.timeSlotRule.insertMany([
            for (timeSlot in slots)
            if (setAvailability(timeSlot) != null)
            {
                startTime: timeSlot.start.toDate(),
                endTime: timeSlot.end.toDate(),
                availability: haxe.Json.parse(tink.Json.stringify(setAvailability(timeSlot))),
            }
        ], {
            update: u -> [u.availability.set(Functions.values(u.availability))],
        })
            .toJsPromise()
            .then(_ -> null);
    }

    static function sendAttendancePoll(chatId:String) {
        var tgBot = new Telegraf(TelegramConfig.tgBotToken);
        var now = Date.now();
        var curType = TimeSlotType.classify(now);
        return CockroachDb.db.getDeliveries(now)
            .next(ds -> ds.filter(d -> TimeSlotType.classify(d.pickupTimeSlot.start) == curType))
            .toJsPromise()
            .then(curDeliveries -> {
                final choices = [
                    "æˆ‘æƒ³é€ (å¦‚æœ‰é›£è™•ï¼Œè«‹ç•™è¨€å¯æ¥å–®æ™‚é–“) âœ‹",
                    "å†‡äººå°±æµæˆ‘é€ (å¦‚æœ‰é›£è™•ï¼Œè«‹ç•™è¨€å¯æ¥å–®æ™‚é–“) ğŸ§˜",
                    "é€å””åˆ° ğŸ™",
                ];
                final opts = {
                    is_anonymous: false,
                    allows_multiple_answers: false,
                };
                switch (curType) {
                    case Lunch:
                        final msg = if (curDeliveries.length <= 0) {
                            'ä»Šæœæš«æ™‚æœªæ”¶åˆ°å–®ã€‚å¦‚æœ12é»å‰æœ‰çªç™¼å–®ï¼Œé‚Šå€‹å¯ä»¥å¹«æ‰‹é€ï¼Ÿ';
                        } else {
                            'ä»Šæœæš«æ™‚æ”¶åˆ° ${curDeliveries.length} å–®ã€‚é‚Šå€‹å¯ä»¥å¹«æ‰‹é€ï¼Ÿ';
                        }
                        tgBot.telegram.sendPoll(chatId, msg, choices, opts)
                            .then(_ -> null);
                    case Dinner:
                        final msg = if (curDeliveries.length <= 0) {
                            'ä»Šæ™šæš«æ™‚æœªæ”¶åˆ°å–®ã€‚å¦‚æœ7é»å‰æœ‰çªç™¼å–®ï¼Œé‚Šå€‹å¯ä»¥å¹«æ‰‹é€ï¼Ÿ';
                        } else {
                            'ä»Šæ™šæš«æ™‚æ”¶åˆ° ${curDeliveries.length} å–®ã€‚é‚Šå€‹å¯ä»¥å¹«æ‰‹é€ï¼Ÿ';
                        }
                        tgBot.telegram.sendPoll(chatId, msg, choices, opts)
                            .then(_ -> null);
                }
            });
    }

    static function main():Void {
        js.Node.exports.sendAttendancePoll = function(evt, context) {
            return sendAttendancePoll(TelegramConfig.internalGroupChatId)
                .then(_ -> {
                    statusCode: 200,
                    body: "done",
                })
                .catchError(err -> {
                    context.serverlessSdk.captureError(err);
                    {
                        statusCode: 500,
                        body: Std.string(err),
                    };
                });
        }
        js.Node.exports.sendDutyPoll = function(evt, context) {
            return sendDutyPoll(TelegramConfig.internalGroupChatId)
                .then(_ -> {
                    statusCode: 200,
                    body: "done",
                })
                .catchError(err -> {
                    context.serverlessSdk.captureError(err);
                    {
                        statusCode: 500,
                        body: Std.string(err),
                    };
                });
        }
        js.Node.exports.disableLunch = function(evt, context) {
            final now:LocalDateString = Date.now();
            // user can pre-order in the future 14 days, see TimeSlotSelectorWidget
            final date = now.deltaDays(14);
            return setTimeSlots(date, date, slot -> {
                switch TimeSlotType.classify(slot.start) {
                    case Lunch: Unavailable(AvailabilityTools.disableMessage);
                    case Dinner: null;
                }
            })
                .then(_ -> {
                    statusCode: 200,
                    body: "done",
                })
                .catchError(err -> {
                    context.serverlessSdk.captureError(err);
                    {
                        statusCode: 500,
                        body: Std.string(err),
                    };
                });
        }

        if (isMain) {
            switch (Sys.args()) {
                case ["sendAttendancePoll"]:
                    sendAttendancePoll(TelegramConfig.internalGroupChatId)
                        .then(_ -> Sys.exit(0))
                        .catchError(err -> {
                            trace(err);
                            Sys.exit(1);
                        });
                case ["sendAttendancePoll", "test"]:
                    sendAttendancePoll(TelegramConfig.testingGroupChatId)
                        .then(_ -> Sys.exit(0))
                        .catchError(err -> {
                            trace(err);
                            Sys.exit(1);
                        });
                case ["sendDutyPoll"]:
                    sendDutyPoll(TelegramConfig.internalGroupChatId)
                        .then(_ -> Sys.exit(0))
                        .catchError(err -> {
                            trace(err);
                            Sys.exit(1);
                        });
                case ["sendDutyPoll", "test"]:
                    sendDutyPoll(TelegramConfig.testingGroupChatId)
                        .then(_ -> Sys.exit(0))
                        .catchError(err -> {
                            trace(err);
                            Sys.exit(1);
                        });
                case ["disableLunch", startDate, endDate]:
                    setTimeSlots(startDate + " 00:00:00", endDate + " 00:00:00", slot -> {
                        switch (TimeSlotType.classify(slot.start)) {
                            case Lunch: Unavailable(AvailabilityTools.disableMessage);
                            case Dinner: null;
                        }
                    })
                        .then(_ -> Sys.exit(0))
                        .catchError(err -> {
                            trace(err);
                            Sys.exit(1);
                        });
                case args:
                    throw "unknown args: " + Json.stringify(args);
            }
        }
    }
}